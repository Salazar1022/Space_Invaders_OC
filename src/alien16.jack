/** Alien de 16x16 en coordenadas de píxeles */
class Alien16 {
    field int x;       // posición X en píxeles (0..511)
    field int y;       // posición Y en píxeles (0..255)
    field Array sprite; // 16 ints, cada uno 16 bits de la fila
    field boolean alive;

    /** Constructor: (x en píxeles, y en píxeles, puntero a sprite[16]) */
    constructor Alien16 new(int xPos, int yStart, Array spr) {
        let x = xPos;
        let y = yStart;
        let sprite = spr;
        let alive = true;
        return this;
    }

    method void draw() {
        var int x2; var int y2;
        var int row; var int bits; var int bit; var int px;
        var int drawX; var int drawY;
        
        let x2 = x + 31;  // Ahora son 32x32
        let y2 = y + 31;
        
        // Solo dibujar si está completamente dentro de la pantalla
        if (x < 0) { return; }
        if (y < 0) { return; }
        if (x2 > 511) { return; }
        if (y2 > 255) { return; }
        
        // Dibujar sprite pixel por pixel, ESCALADO 2x
        do Screen.setColor(true);
        let row = 0;
        while (row < 16) {
            let bits = sprite[row];
            let bit = 0;
            while (bit < 16) {
                // Verificar si el bit está activado
                let px = bits & 1;
                if (px = 1) {
                    // Dibujar pixel 2x2 para hacer el alien más grande
                    let drawX = x + ((15 - bit) * 2);
                    let drawY = y + (row * 2);
                    do Screen.drawPixel(drawX, drawY);
                    do Screen.drawPixel(drawX + 1, drawY);
                    do Screen.drawPixel(drawX, drawY + 1);
                    do Screen.drawPixel(drawX + 1, drawY + 1);
                }
                let bits = bits / 2;  // Shift right
                let bit = bit + 1;
            }
            let row = row + 1;
        }
        return;
    }

    method void clear() {
        var int x2; var int y2;
        if (~alive) { return; }
        
        let x2 = x + 31;  // Ahora son 32x32
        let y2 = y + 31;
        
        // Solo borrar si está completamente dentro de la pantalla
        if (x < 0) { return; }
        if (y < 0) { return; }
        if (x2 > 511) { return; }
        if (y2 > 255) { return; }
        
        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, x2, y2);
        return;
    }

    method void moveX(int dx) {
        // Borro, muevo, dibujo
        do clear();
        let x = x + dx;
        do draw();
        return;
    }

    method void moveDown(int dy) {
        do clear();
        let y = y + dy;
        do draw();
        return;
    }

    method int leftX()  { return x; }
    method int rightX() { return x + 31; }  // Ahora son 32x32
    method int topY()      { return y; }
    method int bottomY()   { return y + 31; }  // Ahora son 32x32

    method void kill() {
        do clear();
        let alive = false;
        return;
    }

    method boolean isAlive() { return alive; }
}
